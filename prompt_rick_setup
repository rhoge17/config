#
# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png
#

function prompt_char {
  git branch >/dev/null 2>/dev/null && echo "±" && return
  echo '○'
}

function box_color {

    # Determine if local or remote

    SESSION_TYPE=local
    THECOLOR="%F{033}"
    if [ -n "$SSH_CLIENT" ] || [ -n "$SSH_TTY" ]; then
	SESSION_TYPE=remote/ssh
	THECOLOR="%F{196}"
    else
	case $(ps -o comm= -p $PPID) in
	    sshd|*/sshd) SESSION_TYPE=remote/ssh;THECOLOR="%F{196}";
	esac
    fi

    #git branch >/dev/null 2>/dev/null && echo "±" && return
    echo $THECOLOR

}

function prompt_rick_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="(${_prompt_rick_colors[1]}%b%f%u%c${_prompt_rick_colors[4]}●%f)"
  else
    branch_format="(${_prompt_rick_colors[1]}%b%f%u%c)"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_rick_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_rick_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_rick_colors=(
      "%F{81}"  # 1 Turquoise
      "%F{166}" # 2 Orange
      "%F{135}" # 3 Purple
      "%F{161}" # 4 Hotpink
      "%F{118}" # 5 Limegreen
      "%F{040}" # 6 Fino Green (user)
      "%F{239}" # 7 Fino Grey (text)
      "%F{033}" # 8 Fino Blue (box)
      "%F{226}" # 9 Fino Yellow (current directory)
      "%F{196}" # 10 Red1 (alternate box)
      "%F{202}" # 11 OrangeRed1 (alternate box)
    )
  else
    _prompt_rick_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_rick_colors[1]}%b%f%u%c)"
  local action_format="(${_prompt_rick_colors[5]}%a%f)"
  local unstaged_format="${_prompt_rick_colors[2]}●%f"
  local staged_format="${_prompt_rick_colors[5]}●%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v)'

  local prompt_char='$(prompt_char)'
  local box_color='$(box_color)'
  
  # Define prompts.
  PROMPT="╭─${_prompt_rick_colors[6]}%n%f ${_prompt_rick_colors[7]}at ${box_color}%m%f ${_prompt_rick_colors[7]}in ${_prompt_rick_colors[9]}%~%f "'${vcs_info_msg_0_}'"
"'$python_info[virtualenv]'"╰─${prompt_char} "
  RPROMPT=''
}

prompt_rick_setup "$@"
